#! /usr/bin/perl

use strict;
use warnings;

use Digest::MD5 qw(md5_hex);
use Getopt::Long;
use Pod::Usage;

our $VERSION = 0.01;

my ($opt_help, $opt_version, $opt_unlink_core);
my $opt_delay = 10;

GetOptions(
    help          => \$opt_help,
    version       => \$opt_version,
    'unlink-core' => \$opt_unlink_core,
) or pod2usage(1);
if ($opt_help) {
    pod2usage(0);
} elsif ($opt_version) {
    print "$VERSION\n";
    exit 0;
}

while (1) {
    # build list of files (with checksum)
    my %core_files = map {
        $_ => md5_file($_)
    } grep {
        ! -e bt_filename($_)
    } <core.*>;
    sleep 5;
    next unless %core_files;
    for my $core_file (sort keys %core_files) {
        # skip if the checksum of the core file has changed
        next if $core_files{$core_file} eq md5_file($core_file);
        # take backtrace
        print "taking backtrace of core file:$core_file\n";
        open(
            my $fh,
            '|-',
            "gdb -batch -c $core_file > @{[bt_filename($core_file)]} 2>&1",
        ) or die "failed to start gdb:$!";
        print $fh "thread apply all bt\n";
        close $fh;
        die "gdb exited with status: $?\n"
            if $? != 0;
        # unlink if necessary
        if ($opt_unlink_core) {
            unlink $core_file
                or die "failed to unlink core file:$core_file:$!";
        }
    }
}

sub bt_filename {
    my $fn = shift;
    $fn =~ s/^core/bt/;
    $fn;
}

sub md5_file {
    my $fn = shift;
    open my $fh, '<', $fn
	or die "failed to open $fn:$!";
    my $md5 = Digest::MD5->new;
    $md5->addfile($fh);
    close $fh;
    $md5->hexdigest;
}

__END__

=head1 NAME

bt_cores - a daemon that takes stack backtrace of core files automatically

=head1 SYNOPSIS

    bt_cores [-u]

=over 4

=item -u, --unlink-core

Removes the core file after taking backtrace.

=back

=head1 DESCRIPTION

Bt_cores is a daemon that waits for core files (that match pattern "core.<pid>") to appear in current directory, and takes backtraces of the images.

=head1 AUTHOR

Kazuho Oku

=cut
